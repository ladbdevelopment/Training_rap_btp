@Metadata.layer: #CORE
@Search.searchable: true // activar busqueda a la vista
@UI: { headerInfo: { typeName: 'Travel',
                     typeNamePlural: 'Travels',
                     title: { type: #STANDARD, 
                              value: 'TravelId' }}}
annotate entity zladb_c_travel
    with 
{
     @UI: {
     facet: [{ id: 'Travel',
               purpose: #STANDARD,
               type: #IDENTIFICATION_REFERENCE,
               label: 'Travel',
               position: 10 },
             { id: 'Booking',
               purpose: #STANDARD,
               type: #LINEITEM_REFERENCE,
               label: 'Booking',
               position: 20,
               targetElement: '_Booking' }],
               
      lineItem: [{ position: 10,
                   importance: #HIGH}], // Depende del dispositivo para renderizar(responsive) para indicar si se muestre o no( movil, Tablet)

      identification: [{ position: 10,
                         label: 'Travel ID [1,...,99999999]' }]}

  @Search.defaultSearchElement: true
  @Search.fuzzinessThreshold: 0.7 // La busqueda muestre el 70% de la palabra de busqueda
  TravelId;

  @UI: { lineItem: [{ position: 20, 
                      importance: #HIGH }],
                      
        identification: [{ position: 20 }],
        
        selectionField: [{ position: 20 }] }
  
  // Ayuda de busqueda por la vista de asociacion de la vista /DMO/I_Agency        
  @Consumption.valueHelpDefinition: [{ entity : {name: '/DMO/I_Agency', 
                                                 element: 'AgencyID' } }]
  AgencyId;
  
  @UI: { lineItem: [{ position: 30, 
                      importance: #HIGH }],
                      
         identification: [{ position: 30 }],
         
         selectionField: [{ position: 30 }] }
  @Consumption.valueHelpDefinition: [{ entity : {name: '/DMO/I_Customer', 
                                                 element: 'CustomerID' }} ]
  @Search.defaultSearchElement: true
  CustomerId;
  
  @UI: { lineItem: [{ position: 40, 
                      importance: #MEDIUM }],
         identification: [{ position: 40 }] }
  BeginDate;
  
  @UI: { lineItem: [{ position: 41, 
                      importance: #MEDIUM }],
         identification: [{ position: 41 }] }
  EndDate;
  
  @UI: { lineItem: [{ position: 42, 
                      importance: #MEDIUM }],
         identification: [{ position: 42 }] }
  BookingFee;
  
  @UI: { lineItem: [{ position: 43, 
                      importance: #MEDIUM }],
                      
         identification: [{ position: 43, 
                            label: 'Total Price' }] }
  TotalPrice;
  
  @Consumption.valueHelpDefinition: [{entity: {name: 'I_Currency', 
                                               element: 'Currency' }}]
  CurrencyCode;
  
  @UI: { lineItem: [{ position: 50, 
                      importance: #HIGH },
                    { type: #FOR_ACTION, 
                      dataAction: 'CreateTravelWithCopy', 
                      label: 'Create Travel by Template' }],
                      
         identification: [{ position: 45, 
                            label: 'Status [O(Open)|A(Accepted)|X(Canceled)]' }] }
  OverallStatus;
  
  @UI: { identification:[{ position: 46 }] }
  Description;
  
  @UI.hidden: true
  Lastchangedat;
  
    @UI: { lineItem: [{ position: 60, 
                      importance: #HIGH }],
           identification: [{ position: 60 }] }
  DiscountPrice;
 
    
}